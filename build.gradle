
// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url "https://github.com/ehn-dcc-development/dgc-business-rules/packages/849466"
        }
    }

    dependencies {
        classpath Deps.tools_gradle_android
        classpath Deps.tools_kotlin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    def getGitHash = { ->
        return "git rev-parse --short HEAD".execute().text.trim()
    }
    def getCommitCount = { ->
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = code
        }
        return Integer.parseInt(code.toString().trim())
    }
    commitCount = getCommitCount()
    gitHash = getGitHash()
}

task buildAllArtifacts() {
    group = 'Build'
    description = 'Builds and copies all maven artifact into a folder that can then be used in another project as local repository'

    dependsOn 'engine:publishAllPublicationsToMavenRepository'

    doLast {
        println 'Copying latest AAR files into local repository'
        delete 'build/local-maven-repo'
        copy {
            from 'engine/build/outputs/maven-artifact'
            include '**'
            into 'build/local-maven-repo'
        }
    }
}